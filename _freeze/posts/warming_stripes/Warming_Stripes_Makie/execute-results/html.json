{
  "hash": "4c2b889c5b8fbeb4b92a02523f021dd6",
  "result": {
    "markdown": "# Tutorial: How to generate your own warming stripes using `Julia`\n\nIn this quick tutorial we learn how to create warming stripes similar to those available at [showyourstripes.info](https://showyourstripes.info).\n\nAlso, this tutorial serves as an opportunity to become familiar with the `Julia` programming language and some of its packages.\n\nOur goal is to get as close as possible to the visual appearance of the famous warming stripes.\n\nWe will use `Julia` to access and visualize annual air temperature data for Germany provided by [Deutscher Wetterdienst](http://opendata.dwd.de).\n\nFirst, some packages for data handling, processing and visualization have to be loaded.\n\nWe will download a large CSV file from the web. To this end, we need the packages `CSV` and `Downloads`. Further, we store the data in some kind of a database provided by `DataFrames`.\n\nFor the processing of the data we need some statistical quantities from the standard `Statistics` package.\n\nThe package `RollingFunctions` provides tools to smooth temporal data using windowed averages.\n\nFinally, we create a custom colormap and plot the data with the `Makie` package.\n\n::: {.cell execution_count=1}\n``` {.julia .cell-code}\nusing CSV\nusing Downloads\nusing DataFrames\n\nusing Statistics\nusing RollingFunctions\n\nusing ColorSchemes\nusing CairoMakie\n```\n:::\n\n\n### Color map\n\nTo get close to the visual style of the warming stripes charts by Ed Hawking, we define a customized colormap.\n\nThe original [color palette from Wikipedia](https://en.wikipedia.org/wiki/Warming_stripes#/media/File:20180522_Color_palette_for_warming_stripes_-_ColorBrewer_9-class_single_hue.svg) provides the required color codes for the blue-white-red colormap.\n\n::: {.cell execution_count=2}\n``` {.julia .cell-code}\nwstripesmap = cgrad([\n        \"#08306b\", \"#08519c\", \"#2171b5\", \"#4292c6\", \n        \"#6baed6\", \"#9ecae1\", \"#c6dbef\", \"#deebf7\",\n        \"#ffffff\",\n        \"#fee0d2\", \"#fcbba1\", \"#fc9272\", \"#fb6a4a\",\n        \"#ef3b2c\", \"#cb181d\", \"#a50f15\", \"#67000d\"],\n    categorical=true);\nwstripesmap\n```\n\n::: {.cell-output .cell-output-display execution_count=3}\n![](Warming_Stripes_Makie_files/figure-html/cell-3-output-1.svg){}\n:::\n:::\n\n\n## Data access\n\nWe load data from a URL and store everything in a database structure known as a *DataFrame*.\n\n::: {.cell execution_count=3}\n``` {.julia .cell-code}\nurl = \"https://opendata.dwd.de/climate_environment/CDC/regional_averages_DE/annual/air_temperature_mean/regional_averages_tm_year.txt\"\nhttp_response = Downloads.download(url)\ndf = CSV.File(http_response, header=2, delim=\";\") |> DataFrame;\n```\n:::\n\n\n## Baseline data and long-term statistics\n\nAs a baseline we need the average over sufficiently many years. We choose temperature data from the years 1971 to 2000 and calculate the mean temperature.\n\nFurther, we need the standard deviation of the long-term temperature data from 1901 to 2000.\n\nA smoothed data set based on a running mean is generated with an averaging window of 30 years length.\n\n::: {.cell execution_count=4}\n``` {.julia .cell-code}\ndf_baseline = filter(row -> (row.Jahr > 1970) & (row.Jahr < 2001), df)\nwhere = \"Deutschland\"\ncol = df_baseline[!, where];\nmw = mean(col)\n\ndf_longterm = filter(row -> (row.Jahr > 1900) & (row.Jahr < 2001), df)\ncol = df_longterm[!, where];\nsd = std(col)\n\ndata = df[!, where] .- mw;\nm_data = runmean(data, 30);\n```\n:::\n\n\n## Visualization\n\nIn the following code cell, we use `Makie` to generate a bar chart with annual temperature data relative to the baseline average of 1971 to 2000. The colormap is scaled to +/- 2.6 times the standard deviation of the long-term data from 1901 to 2000 (see *How have these graphics been made?* in the [FAQ at showyourstripes.info](https://showyourstripes.info/faq)).\n\nWe add as white line the smoothed temperature obtained by calculating running averages with a time window of 30 years length.\n\nThere is no doubt that the average temperature relative to the baseline data is approaching far more than 1.5 K (or 1.5 Â°C as it is commonly communicated) for the recent two decades since year 2000.\n\n::: {.cell execution_count=5}\n``` {.julia .cell-code}\nset_theme!(theme_black())\nfig = Figure(resolution = (1000, 540), fonts = (; regular= \"sans\"))\nax = Axis(fig[1, 1], \n    title = \"Temperature change in Germany relative to average of 1970-2001\",\n    ylabel = \"K\",\n    xautolimitmargin = (0.02,0.02),\n    yautolimitmargin = (0.02,0.02))\nbarplot!(ax, df.Jahr, data; width=1, gap=0, \n    strokewidth = 0.5, strokecolor = data,\n    color = data, \n    colorrange = (-2.6 * sd, +2.6 * sd),\n    colormap = wstripesmap)\nlines!(ax, df.Jahr, m_data, color=:white)\ntext!(\"Data source: http://opendata.dwd.de\\n\" * \n    \"https://showyourstripes.info by Ed Hawkins, University of Reading, UK\", \n    fontsize=10,\n    align=(:left, :center),\n    position = (1880, -1.84))\ntext!(\"The white curve indicates a running average\\n\" *\n    \"with a window length of 30 years.\",\n    fontsize = 10,\n    align=(:left, :center),\n    position=(1990, -1.84))\nfig\n```\n\n::: {.cell-output .cell-output-display execution_count=6}\n![](Warming_Stripes_Makie_files/figure-html/cell-6-output-1.svg){}\n:::\n:::\n\n\nThe next cell generates the well-known warming stripes for Germany.\n\n::: {.cell execution_count=6}\n``` {.julia .cell-code}\nset_theme!(theme_minimal())\nfig = Figure(resolution = (780, 120))\nax = Axis(fig[1, 1],\nbackgroundcolor = :transparent,\n        leftspinevisible = false,\n        rightspinevisible = false,\n        bottomspinevisible = false,\n        topspinevisible = false,\n        xticklabelsvisible = false, \n        yticklabelsvisible = false,\n        xgridcolor = :transparent,\n        ygridcolor = :transparent,\n        xminorticksvisible = false,\n        yminorticksvisible = false,\n        xticksvisible = false,\n        yticksvisible = false,\n        xautolimitmargin = (0.0,0.0),\n        yautolimitmargin = (0.0,0.0),)\nhidedecorations!(ax)\nax.titlevisible=false\nhmap = heatmap!(df.Jahr, ones(size(df.Jahr)), data;\n    colorrange = (-2.6 * sd, +2.6 * sd), \n    colormap = wstripesmap)\nfig\n```\n\n::: {.cell-output .cell-output-display execution_count=7}\n![](Warming_Stripes_Makie_files/figure-html/cell-7-output-1.svg){}\n:::\n:::\n\n\n",
    "supporting": [
      "Warming_Stripes_Makie_files"
    ],
    "filters": [],
    "includes": {}
  }
}